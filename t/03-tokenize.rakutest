use lib 'lib';

use Test;
use HomoGlypher;

plan 5;

my %cyrillic = (
    'a' => [ 'а' ],
    'b' => [ 'б', 'ь' ],
    'io' => [ 'ю' ]
);

my %myanmar = (
    'oo' => [ 'က']
);

my $hg = HomoGlypher.new;

my $token = $hg.tokenize( 'bar' );
isa-ok $token, Regex, 'token built';

nok 'bar' ~~ $token, 'literal text is not matched';

$hg.add-mapping( %cyrillic );
$hg.add-mapping( %myanmar );
$token = $hg.tokenize( 'bariooo' );
ok [and]( $hg.unwind( 'bariooo' ).map( { $_ ~~ $token } ) ), 'homoglyphed text is matched';

$token = $hg.tokenize( 'foo' );
'fက' ~~ / $<result>=<$token> /;
is $/{ 'result' }, 'fက', 'token captures homoglyphed text';

my $token-short = $hg.tokenize( 'foo' );
my $token-long = $hg.tokenize( 'foobar' );
ok 'xfကьаry' ~~ / 'x' [ <$token-short> | <$token-long> ] 'y' /, 'whole tokens can be backtracked';
